apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.simplemobiletools.smsmessenger"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 34
        versionName "5.9.4"
        setProperty("archivesBaseName", "sms-messenger")
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "brand"
    productFlavors {
        liberty {
            dimension "brand"
            applicationId "io.liberty.messenger"
            versionCode 1
            versionName "1.0.0"
            setProperty("archivesBaseName", "liberty-messenger")
        } 
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        liberty.java.srcDir 'src/liberty/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

configurations.all {
    // Check 'changing' modules for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation ('io.liberty.smtf:simple-commons:1.0.1') { changing = true }
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'com.klinkerapps:android-smsmms:5.2.6'
    implementation 'com.github.tibbi:IndicatorFastScroll:c3de1d040a'
    implementation "me.leolin:ShortcutBadger:1.1.22"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.room:room-runtime:2.3.0"
    annotationProcessor "androidx.room:room-compiler:2.3.0"
}
